apply plugin: 'liberty'

description = "Liberty OAuthStore BELL Sample repo"
jar.baseName = 'oauthstore-bell-sample'

def wlpRoot = "${buildDir}/wlp"
def wlpServerName = "server1"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.4.+'
    }
}

repositories {
    mavenCentral()

    // TODO: Can remove this after 18004 is published to maven central.
    flatDir {
        dirs "${wlpRoot}/dev/api/ibm/"
    }
}

dependencies {
    compile 'org.mongodb:mongodb-driver-sync:3.9.1'

// TODO Can remove the first line and uncomment out the second when 18004 is published to maven central.
    compileOnly name: 'com.ibm.websphere.appserver.api.oauth_1.2.23'
//    compileOnly group: 'com.ibm.websphere.appserver.features', name: 'oauth-2.0', version: '18.0.0.4'

//
// The ci.gradle doc says this should grab and install the feature, but it doesn't work. Maybe b/c I am using the beta?
//
//    libertyFeature 'com.ibm.websphere.appserver.features:oauth-2.0:18.0.0.4'
//    libertyRuntime group: 'io.openliberty', name: 'openliberty-runtime', version: '[18.0.0.4,)'
}

ext {
    httpPort = 8080
    httpsPort = 8443
}

liberty {
    server {
        /* Install all features configured in the server.xml. */
        features {
            acceptLicense = true
        }

        name = "${wlpServerName}"
        bootstrapProperties = ['httpPort': httpPort, 'httpsPort': httpsPort]
        configDirectory = file('src/liberty/config')
    }

    install {
        // TODO: Run with the publicly available November 2018 beta for now.
        runtimeUrl = "https://public.dhe.ibm.com/ibmdl/export/pub/software/websphere/wasdev/downloads/wlp/beta/wlp-beta-2018.11.0.0.zip"
    }
}

libertyStart.doFirst {
    /*
     * Copy the MongoDB properties file to the server.
     */
    copy {
        from "${rootDir}/SupportFiles/mongoDB.props"
        into "${wlpRoot}/usr/servers/${wlpServerName}"
    }
}

jar {
    /*
     * Use our manifest and update the classpath for the bundle to include any runtime dependencies.
     */
    manifest {
    	from(['src/main/resources/META-INF/MANIFEST.MF'])
    }
}

/*
 * This task will copy all runtime JARs required for the OAuthStore implementation.
 */
task copyRuntimeJars(type: Copy) {
    dependsOn jar

    from "${jar.archivePath}"
    from configurations.runtime
    into "${wlpRoot}/usr/shared/resources/libs"
}

/*
 * Install any necessary features that weren't installed in the beta.
 *
 * TODO: This can be removed after the feature install of the Liberty gradle plugin works.
 */
installLiberty.doLast {
    exec {
        commandLine "${wlpRoot}/bin/installUtility", "install", "oauth-2.0", "bells-1.0"
	ignoreExitValue = true
    }
}

// TODO: Need the Liberty install for the OAuthStore API. Can remove when 18004 is published to maven central
compileJava.dependsOn 'installLiberty'

/*
 * Targets for starting and stopping the Liberty server.
 */
clean.dependsOn 'libertyStop'
libertyPackage.dependsOn 'libertyStop'
libertyStart.dependsOn 'libertyStop'
libertyStart.dependsOn 'copyRuntimeJars'
libertyRun.dependsOn 'libertyStop'

task start { dependsOn 'libertyStart' }
task stop  { dependsOn 'libertyStop' }

apply plugin: 'liberty'
apply plugin: 'eclipse'

description = "Liberty OAuthStore BELL Sample repo"
jar.baseName = 'oauthstore-bell-sample'

def wlpRoot = "${buildDir}/wlp"
def wlpServerName = "server1"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.4.+'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.mongodb:mongodb-driver-sync:3.9.1'
    compileOnly group: 'com.ibm.websphere.appserver.api', name: 'com.ibm.websphere.appserver.api.oauth', version: '[1.2.23,)'
    libertyRuntime group: 'com.ibm.websphere.appserver.runtime', name: 'wlp-kernel', version: '[18.0.0.4,)'
}

ext {
    httpPort = 8080
    httpsPort = 8443
}

liberty {
    server {
        features {
            name = ['bells-1.0', 'openidconnectserver-1.0']
            acceptLicense = true
        }

        name = "${wlpServerName}"
        bootstrapProperties = ['httpPort': httpPort, 'httpsPort': httpsPort]
        configDirectory = file('src/liberty/config')
    }
}

libertyStart.doFirst {
	
    /*
     * Copy all runtime JARs required for the OAuthStore implementation.
     */
    copy {
        from "${jar.archivePath}"
        from configurations.runtime
        into "${wlpRoot}/usr/shared/resources/libs"
    }

    /*
     * Copy the MongoDB properties file to the server.
     */
    copy {
        from "${rootDir}/SupportFiles/mongoDB.props"
        into "${wlpRoot}/usr/servers/${wlpServerName}"
    }
}

jar {
    /*
     * Use our manifest and update the classpath for the bundle to include any runtime dependencies.
     */
    manifest {
    	from(['src/main/resources/META-INF/MANIFEST.MF'])
    }
}

/*
 * Targets for starting and stopping the Liberty server.
 */
clean.dependsOn 'libertyStop'
libertyPackage.dependsOn 'libertyStop'
libertyStart.dependsOn 'libertyStop', 'jar'
libertyRun.dependsOn 'libertyStop'

task start { dependsOn 'libertyStart' }
task stop  { dependsOn 'libertyStop' }
